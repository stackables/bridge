version 1.4

# Tool 1: Geocoder (City Name -> Coordinates)
tool geo from std.httpCall {
  .baseUrl = "https://nominatim.openstreetmap.org"
  .method = GET
  .path = /search
  .cache = 60
  # Required by Nominatim policy
  .headers.User-Agent = "TheBridgeDemo/1.0"
}

# Tool 2: Weather (Coordinates -> Temperature)
tool weather from std.httpCall {
  .baseUrl = "https://api.open-meteo.com/v1"
  .method = GET
  .path = /forecast
  .cache = 60
}

# Nominatim returns an array — pick the first result
tool first from std.pickFirst

# Reusable definition to get weather by coordinates
define weatherByCoordinates {
  with weather as w
  with input as i
  with output as o

  w.latitude  <- i.lat
  w.longitude <- i.lon
  w.current_weather = true

  o.lat         <- i.lat
  o.lon         <- i.lon
  o.currentTemp <- w.current_weather.temperature ?? 0.0
  o.timezone    <- w.timezone ?? "UTC"
  o.city        <- i.cityName
}

# By city name: geocode → pick first → weather
bridge Query.getWeatherByName {
  with geo as g
  with weatherByCoordinates as w
  with first as f
  with input as i
  with output as o

  g.q <- i.cityName
  g.format = "json"

  f.in <- g

  w.lat  <- f.lat
  w.lon <- f.lon
  w.cityName <- i.cityName

  o <- w
}

# By coordinates: straight to weather API
bridge Query.getWeatherByCoordinates with weatherByCoordinates

# All optional params:
# if lat/lon provided skip geocode
# if name is requested and name is not provided Unknown
# nothing provided -> error?
bridge Query.getWeather {
  with geo as g
  with weatherByCoordinates as w
  with first as f
  with input as i
  with output as o
  with std.upperCase as upper

  g.q <- i.cityName # Only used if lat/lon not provided
  g.format = "json"

  f.in <- g

  w.lat  <- i.lat || f.lat
  w.lon <- i.lon || f.lon
  w.cityName <- upper:i.cityName || f.display_name || "Unknown"

  o <- w
}