# Tool 1: Geocoder (City Name -> Coordinates)
extend std.httpCall as geo {
  baseUrl = "https://nominatim.openstreetmap.org"
  method = GET
  path = /search
  cache = 60
  # Required by Nominatim policy
  headers.User-Agent = "TheBridgeDemo/1.0" 
}

# Tool 2: Weather (Coordinates -> Temperature)
extend std.httpCall as weather {
  baseUrl = "https://api.open-meteo.com/v1"
  method = GET
  path = /forecast
  cache = 60
}

# Nominatim returns an array — pick the first result
extend std.pickFirst as first

# By city name: geocode → pick first → weather
bridge Query.getWeatherByName {
  with geo as g
  with first as f
  with weather as w
  with input as i
  with output as o

  g.q <- i.cityName
  g.format = "json"

  f.in <- g

  w.latitude  <- f.lat
  w.longitude <- f.lon
  w.current_weather = true

  o.city        <- i.cityName
  o.lat         <- f.lat
  o.lon         <- f.lon
  o.currentTemp <- w.current_weather.temperature ?? 0.0
  o.timezone    <- w.timezone ?? "UTC"
}

# By coordinates: straight to weather API
bridge Query.getWeatherByCoordinates {
  with weather as w
  with input as i
  with output as o

  w.latitude  <- i.lat
  w.longitude <- i.lon
  w.current_weather = true

  o.lat         <- i.lat
  o.lon         <- i.lon
  o.currentTemp <- w.current_weather.temperature ?? 0.0
  o.timezone    <- w.timezone ?? "UTC"
}